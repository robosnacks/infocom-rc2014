;Copyright 1982 Infocom, Inc.  All rights reserved.
;Configuration Program for INTERLOGIC(tm) CP/M Files
;Configured for the VT100
;This program should be edited to specify terminal and
;printer parameters for your CP/M system.  Edit this
;file, then assemble, load, and run the program.
;Complete documentation of the setup procedure will
;be found in your INTERLOGIC Reference Card.
;Terminal and printer specific parameters start at
;the label CPMCPL, about 20 lines below.  Nothing
;prior to that label should be altered.
 ORG 100H
LPDL EQU 100H
BDOS EQU 05H
FCB EQU 05CH
PSTRNG EQU 9
OPEN EQU 15
CLOSE EQU 16
WRITES EQU 21
SETDMA EQU 26
START:  jp CPMSTR
;Setup parameters begin here.  Refer to the INTERLOGIC
;Reference Card for CP/M for complete documentation for
;editing this file.
CPMCPL: DB 80  ;Characters/line (132 maximum)
CPMLPP: DB 23  ;Lines/screen (NOT including status line)
CPMFN: DB 'ZORK3   '
   ;File name (DO NOT CHANGE THIS)
CPMCLF: DB 1  ;1 if LF should print after CR on screen
CPMLLF: DB 1  ;1 if LF should print after CR on printer
CPMINV: DB 0  ;Number to add to ASCII value of characters
   ;to have them print in inverse video.
   ;This is not applicable on many terminals.
;The following strings have a count field followed by up
;to 32 characters.
TINIT: DB 18  ;Initialize the terminal.  
   ;Should position the cursor at the
   ;lower left corner of the screen.
   ;On terminals with the facility, a
   ;scrolling region should be established
   ;to include all but the top line on
   ;the screen.  Also, clearing the screen
   ;is desirable.
 DB 1BH,'[2;24r',1BH,'[24;1H',1BH,'[2J'
 DS 32-$+TINIT+1
TRESET: DB 2  ;Reset the terminal at end of game.
   ;For example, if a scrolling region
   ;was established, reset it to include the
   ;entire screen.  Clearing the screen is
   ;NOT desirable.   
 DB 1BH,'c'
 DS 32-$+TRESET+1
BLINE:  DB 10  ;Begin status line.
   ;Should move the cursor to the upper
   ;left corner of the screen.  If at all
   ;possible, should go into inverse video.
 DB 1BH,'[1;1H',1BH,'[7m'
 DS 32-$+BLINE+1
ELINE:  DB 11  ;End status line.
   ;Should move the cursor to the lower
   ;left corner of the screen.  If status
   ;line uses inverse video, this state
   ;should be cleared.
 DB 1BH,'[0m',1BH,'[24;1H'
 DS 32-$+ELINE+1 
PINIT:  DB 0  ;Printer initialization.
   ;If any setup is required prior to
   ;using scripting.
 DS 200H-$
;The setup parameters end at this point.  Nothing below this point
;should be changed for any reason.
CPMSTR: ld sp,PDL+LPDL
 ld a,(CPMCPL)
 cp a,132
 jp c,CPMST1
 ld a,132
 ld (CPMCPL),a
CPMST1: ld hl,FCB
 ld b,36
L1: ld (hl),0
 inc hl
 dec b
 jp nz,L1
 ld hl,FCB+1
 ld de,CPMFN
 ld b,8
L2: ld a,(de)
 ld (hl),a
 inc hl
 inc de
 dec b
 jp nz,L2
 ld de,EXT
 ld b,3
L3: ld a,(de)
 ld (hl),a
 inc hl
 inc de
 dec b
 jp nz,L3
 ld c,OPEN
 ld de,FCB
 call BDOS
 inc a
 ld de,BADOPN
 jp z,FINIS
 ld de,100H
 call WRITEB
 ld de,180H
 call WRITEB
 ld c,CLOSE
 ld de,FCB
 call BDOS
 inc a
 ld de,BADCLS
 jp z,FINIS
 ld de,OK
FINIS: ld c,PSTRNG
 call BDOS
  jp 0
WRITEB: ld c,SETDMA
 call BDOS
 ld c,WRITES
 ld de,FCB
 call BDOS
 or a,a
 ret z
 ld c,CLOSE
 ld de,FCB
 call BDOS
 ld de,BADWRT
  jp FINIS
BADOPN: DB 0DH,0AH,'CAN NOT OPEN FILE$'
BADCLS: DB 0DH,0AH,'ERROR ON FILE CLOSE$'
BADWRT: DB 0DH,0AH,'WRITE ERROR$'
OK: DB 0DH,0AH,'CONFIGURATION PROGRAM FINISHED',0DH,0AH,'$'
EXT: DB 'COM'
PDL: DS LPDL
END

